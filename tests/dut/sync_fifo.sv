// domain SysDomain
//   freq_mhz: 100

module TxQueue #(
  parameter int  DEPTH      = 16,
  parameter int  DATA_WIDTH = 8
) (
  input logic clk,
  input logic rst,
  input logic push_valid,
  output logic push_ready,
  input logic [DATA_WIDTH-1:0] push_data,
  output logic pop_valid,
  input logic pop_ready,
  output logic [DATA_WIDTH-1:0] pop_data,
  output logic full,
  output logic empty
);

  localparam int PTR_W = $clog2(DEPTH) + 1;
  
  logic [DATA_WIDTH-1:0] mem [0:DEPTH-1];
  logic [PTR_W-1:0]     wr_ptr;
  logic [PTR_W-1:0]     rd_ptr;
  
  // Full when MSBs differ and lower bits match
  assign full        = (wr_ptr[PTR_W-1] != rd_ptr[PTR_W-1]) &&
                       (wr_ptr[PTR_W-2:0] == rd_ptr[PTR_W-2:0]);
  assign empty       = (wr_ptr == rd_ptr);
  assign push_ready  = !full;
  assign pop_valid   = !empty;
  assign pop_data    = mem[rd_ptr[PTR_W-2:0]];
  
  always_ff @(posedge clk) begin
    if (rst) begin
      wr_ptr <= '0;
      rd_ptr <= '0;
    end else begin
      if (push_valid && push_ready) begin
        mem[wr_ptr[PTR_W-2:0]] <= push_data;
        wr_ptr <= wr_ptr + 1;
      end
      if (pop_valid && pop_ready) begin
        rd_ptr <= rd_ptr + 1;
      end
    end
  end

endmodule

